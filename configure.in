dnl Initialization

AC_INIT(src/gtkhtml.c)
AM_CONFIG_HEADER(config.h)

PACKAGE=gtkhtml

GTKHTML_RELEASE=3.0
AC_DEFINE_UNQUOTED(GTKHTML_RELEASE, "${GTKHTML_RELEASE}")
AC_SUBST(GTKHTML_RELEASE)

GTKHTML_RELEASE_STRING=${PACKAGE}-${GTKHTML_RELEASE}
AC_DEFINE_UNQUOTED(GTKHTML_RELEASE_STRING, "${GTKHTML_RELEASE_STRING}")
AC_SUBST(GTKHTML_RELEASE_STRING)

AM_INIT_AUTOMAKE($PACKAGE, $GTKHTML_RELEASE.0.99)

EDITOR_API_VERSION=3.0
AC_DEFINE_UNQUOTED(EDITOR_API_VERSION, "${EDITOR_API_VERSION}")
AC_SUBST(EDITOR_API_VERSION)

AM_MAINTAINER_MODE

dnl
dnl Due to the sed scripts being split on 90 line
dnl blocks, this macro needs to be right at the beggining.
dnl
AC_PROG_INTLTOOL

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

if test -f $srcdir/po/Makefile.in.in ; then
   cp $srcdir/po/Makefile.in.in $srcdir/po/Makefile.in.in.bak
   sed -e  "s/\@PACKAGE\@/\@GTKHTML_RELEASE_STRING\@/" $srcdir/po/Makefile.in.in.bak > $srcdir/po/Makefile.in.in
else
    AC_MSG_ERROR([
""
"Can not find Makefile: $srcdir/po/Makefile.in.in"])
fi

ALL_LINGUAS="az cs da de el es et eu fi fr gl hu it ja ko lt lv ms nl nn no pl pt pt_BR ru sk sl sv tr uk vi zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

GAIL_MODULES="gail >= 0.13"
PKG_CHECK_MODULES(GAIL, $GAIL_MODULES)
AC_SUBST(GAIL_CFLAGS)
AC_SUBST(GAIL_LIBS)
AC_SUBST(GAIL_MODULES)

GTKHTML_MODULES="libgnomeui-2.0 >= 1.112.1 libgnomeprint-2.0 >= 1.111.0"
PKG_CHECK_MODULES(GTKHTML, $GTKHTML_MODULES)
AC_SUBST(GTKHTML_CFLAGS)
AC_SUBST(GTKHTML_LIBS)
AC_SUBST(GTKHTML_MODULES)

CAPPLET_MODULES="${GTKHTML_MODULES} libglade-2.0 >= 1.99.9"
PKG_CHECK_MODULES(CAPPLET, ${CAPPLET_MODULES})
AC_SUBST(CAPPLET_CFLAGS)
AC_SUBST(CAPPLET_LIBS)

EDITOR_MODULES="${GTKHTML_MODULES} libbonoboui-2.0 >= 1.112.1 gal-2.0 >= 0.0.2"
PKG_CHECK_MODULES(EDITOR, $EDITOR_MODULES)
AC_SUBST(EDITOR_CFLAGS)
AC_SUBST(EDITOR_LIBS)

ORBIT_IDL="`$PKG_CONFIG --variable=orbit_idl ORBit-2.0`"
AC_SUBST(ORBIT_IDL)

BONOBO_IDL_INCLUDES="-I`$PKG_CONFIG --variable=idldir libbonobo-2.0` -I`$PKG_CONFIG --variable=idldir bonobo-activation-2.0`"
AC_SUBST(BONOBO_IDL_INCLUDES)

dnl **********************************
dnl Editor component
dnl **********************************

EDITOR_API_VERSION=3.0
AC_DEFINE_UNQUOTED(EDITOR_API_VERSION, "${EDITOR_API_VERSION}")
AC_SUBST(EDITOR_API_VERSION)

if test "x$shlib_factory" = "xyes"; then
	GNOME_GTKHTML_EDITOR_SHLIB=1
	GNOME_GTKHTML_EDITOR_TYPE="shlib"
	GNOME_GTKHTML_EDITOR_LOCATION="libgnome-gtkhtml-editor-${EDITOR_API_VERSION}.so"
	AC_DEFINE(GNOME_GTKHTML_EDITOR_SHLIB)
else
	GNOME_GTKHTML_EDITOR_LOCATION="gnome-gtkhtml-editor-${EDITOR_API_VERSION}"
	GNOME_GTKHTML_EDITOR_TYPE="exe"
fi

AC_SUBST(GNOME_GTKHTML_EDITOR_TYPE)
AC_SUBST(GNOME_GTKHTML_EDITOR_LOCATION)
AM_CONDITIONAL(EDITOR_SHLIB, test x$shlib_factory = xyes)

dnl **********************************
dnl Library version information.
dnl **********************************
 
dnl Increment the following if the interface has additions, changes,
dnl removals.
GTKHTML_CURRENT=0

dnl Increment any time the source changes; set to 0 if you
dnl increment CURRENT.
GTKHTML_REVISION=0

dnl Increment if any interfaces have been added; set to 0
dnl if any interfaces have been removed. removal has 
dnl precedence over adding, so set to 0 if both happened.
GTKHTML_AGE=0

AC_SUBST(GTKHTML_CURRENT)
AC_SUBST(GTKHTML_REVISION)
AC_SUBST(GTKHTML_AGE)

dnl **************
dnl Done.
dnl **************

AC_OUTPUT([
Makefile
gtkhtml.spec
po/Makefile.in
src/Makefile
src/libgtkhtml.pc
a11y/Makefile
capplet/Makefile
components/Makefile
components/html-editor/Makefile
components/html-editor/GNOME_GtkHTML_Editor.server.in
art/Makefile
])

echo "

Configuration:

	Editor component type:		${GNOME_GTKHTML_EDITOR_TYPE}
"
