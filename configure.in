dnl Initialization

AC_INIT(src/gtkhtml.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gtkhtml, 0.7)

AM_MAINTAINER_MODE
AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT(capplet)

AC_PROG_CC
AC_PROG_CXX
AC_ISC_POSIX
AC_HEADER_STDC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

ALL_LINGUAS="cs de fr gl ja ko nl no pt_BR ru sl sv tr uk"
AM_GNOME_GETTEXT

dnl ******************************
dnl Gnome App Lib checking
dnl ******************************
AC_MSG_CHECKING(for Gnome App libraries (GAL) >= 0.2)
if gnome-config --libs gal > /dev/null 2>&1; then 
    vers=`gnome-config --modversion gal | sed -e "s/gal-//" -e 's/cvs$//' -e 's/pre$//' | \
        awk 'BEGIN { FS = "."; } { print $1 * 1000 + $2;}'`
    if test "$vers" -ge 2; then
        AC_MSG_RESULT(found)
    else
        AC_MSG_ERROR(You need at least GNOME Application libs 0.2 for this version of GtkHTML)
    fi
else
    AC_MSG_ERROR(Did not find GnomeAppLib (GAL) installed)
fi

dnl ******************************
dnl capplet library check
dnl ******************************
AC_MSG_CHECKING(for capplet libraries)
if gnome-config --libs capplet > /dev/null 2>&1; then
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(Did not find capplet library installed, you need to install control center)
fi

dnl *****************************
dnl GNU regex checking
dnl *****************************

AC_CHECK_FUNC(re_compile_pattern, AC_DEFINE(HAVE_GNU_REGEX))

dnl glibwww checks.
have_glibwww=false
AC_MSG_CHECKING(for glibwww >= 0.2)
if gnome-config --libs glibwww > /dev/null 2>&1; then
	vers=`gnome-config --modversion glibwww | sed -e "s/glibwww-//" | \
		awk 'BEGIN { FS = "."; } { printf "%d", $1 * 1000 + $2;}'`

	if test "$vers" -ge 2; then
		AC_MSG_RESULT([found])

		GLIBWWW_CFLAGS="`gnome-config --cflags glibwww`"
		GLIBWWW_LIBS="`gnome-config --libs glibwww`"

		have_glibwww=true
	else
		AC_MSG_RESULT(not found)
	fi
else
	AC_MSG_RESULT(not found)
fi

if $have_glibwww ; then
	AC_DEFINE(HAVE_GLIBWWW)
else
	AC_MSG_WARN([\`testgtkhtml' will not be built.])
fi

AC_SUBST(GLIBWWW_CFLAGS)
AC_SUBST(GLIBWWW_LIBS)
AM_CONDITIONAL(GLIBWWW, test x$have_glibwww = xtrue)

dnl Bonobo checks.

try_bonobo=true
bonobo=
bonobo_msg=no
have_bonobo=false

AC_ARG_WITH(bonobo,
	[--{with,without}-bonobo    Compile with Bonobo support or without it],
	if test x$withval = xno; then
		try_bonobo=false
	fi
	if test x$withval = xyes; then
		try_bonobo=true
	fi
)

if $try_bonobo; then
	AC_MSG_CHECKING(for Bonobo >= 0.23)

	if gnome-config --libs bonobox > /dev/null 2>&1; then
		vers=`gnome-config --modversion bonobo | sed -e "s/bonobo-//" | \
			awk 'BEGIN { FS = "."; } { printf "%d", $1 * 1000 + $2;}'`
		if test "$vers" -ge 23; then
			bonobo_ok=true
		else
			bonobo_ok=false
		fi
	else
		bonobo_ok=false
	fi
	
	if $bonobo_ok; then
		AC_MSG_RESULT([found ("$vers")])
		AC_DEFINE(ENABLE_BONOBO)
		have_bonobo=true
		if gnome-config --libs bonobox > /dev/null 2>&1; then
			bonobo=bonobox
		else
			bonobo=bonobo
		fi
		bonobo_msg=yes
	else
		AC_MSG_RESULT([not found
***************
*** WARNING ***  gtkhtml will be build without HTML Editor component
***************  which is required for Evolution])
	fi
fi

AM_CONDITIONAL(BONOBO, $have_bonobo)

dnl Check if Bonobo is OAFized
AC_MSG_CHECKING(if Bonobo uses OAF)
if ( gnome-config --libs bonobo | grep oaf ) > /dev/null 2>&1 ; then
	using_oaf="yes"
	AC_DEFINE(USING_OAF)
else
	using_oaf="no"
fi

AC_MSG_RESULT("$using_oaf")

AM_CONDITIONAL(USING_OAF, test "x$using_oaf" = "xyes")

dnl gconf checks

try_gconf=false
have_gconf=false

AC_ARG_WITH(gconf,
	[--{with,without}-gconf    Compile with Gconf support or without it],
	if test x$withval = xno; then
		try_gconf=false
	fi
	if test x$withval = xyes; then
		try_gconf=true
	fi
)

GCONF_CFLAGS=""
GCONF_LIBS=""
if $try_gconf; then
	AC_MSG_CHECKING([for gconf])
	if gconf-config --version > /dev/null 2>&1; then
		gconf_ver=`gconf-config --version`
		have_gconf=true
		GCONF_CFLAGS="`gconf-config --cflags gconf-gtk` -DGTKHTML_HAVE_GCONF"
		GCONF_LIBS="`gconf-config --libs gconf-gtk`"
		AC_MSG_RESULT([found ($gconf_ver)])
	else
		have_gconf=false
		AC_MSG_RESULT([not found, gtkhtml will be built without gconf support])
	fi
fi
AC_SUBST(GCONF_CFLAGS)
AC_SUBST(GCONF_LIBS)

dnl pspell checks

try_pspell=true
have_pspell=false

AC_ARG_WITH(pspell,
	[--{with,without}-pspell    Compile with Pspell support or without it],
	if test x$withval = xno; then
		try_pspell=false
	fi
	if test x$withval = xyes; then
		try_pspell=true
	fi
)

PSPELL_CFLAGS=""
PSPELL_LIBS=""
if $try_pspell; then
	AC_MSG_CHECKING([for pspell])
	if pspell-config version > /dev/null 2>&1; then
		pspell_ver=`pspell-config version`
		have_pspell=true
		PSPELL_CFLAGS="-DGTKHTML_HAVE_PSPELL"
		if test x$CC = xgcc; then
			PSPELL_LIBS="-lpspell -lstdc++"
		else
			PSPELL_LIBS="-lpspell"
		fi
		AC_MSG_RESULT([found ($pspell_ver)])
	else
		have_pspell=false
		AC_MSG_RESULT([not found, gtkhtml will be built without pspell support])
	fi
fi
AC_SUBST(PSPELL_CFLAGS)
AC_SUBST(PSPELL_LIBS)

AM_CONDITIONAL(PSPELL, $have_pspell)

dnl ******************************
dnl GnomePrint checking
dnl ******************************
AC_MSG_CHECKING(for GnomePrint libraries >= 0.24)
if gnome-config --libs print > /dev/null 2>&1; then 
    vers=`gnome-config --modversion print | sed -e "s/gnome-print-//" -e 's/cvs$//' -e 's/pre$//' | \
        awk 'BEGIN { FS = "."; } { print $1 * 1000 + $2;}'`
    if test "$vers" -ge 24; then
        AC_MSG_RESULT(found)
    else
        AC_MSG_ERROR(You need at least GNOME print 0.21pre for this version of GAL)
    fi
else
    AC_MSG_ERROR(Did not find GnomePrint installed)
fi


dnl gdk-pixbuf checks.

AC_MSG_CHECKING([for gdk-pixbuf >= 0.8.0])

if gnome-config --libs gdk_pixbuf > /dev/null 2>&1; then
	vers=`gnome-config --modversion gdk_pixbuf | sed 's/^gdk-pixbuf-//'`
        major=`echo "$vers" | sed 's/\([[0-9]]*\)\.[[0-9]]*\.[[0-9]]*/\1/'`
        minor=`echo "$vers" | sed 's/[[0-9]]*\.\([[0-9]]*\)\.[[0-9]]*/\1/'`

	if [[ "x$major" = "x" ]] ; then
		gdkpixbuf_ok=false
        elif [[ "x$major" = "x0" ]] ; then
		case "$minor" in
			[[01234567]])
				gdkpixbuf_ok=false ;;
                        *)
				gdkpixbuf_ok=true ;;
		esac
	else
		gdkpixbuf_ok=true
	fi
	AC_MSG_RESULT([found ("$vers")])
else
	gdkpixbuf_ok=false
	AC_MSG_RESULT([not found])
fi

if test x$gdkpixbuf_ok = xfalse ; then
	AC_MSG_ERROR([gdk-pixbuf 0.8.0 or later is required to compile GtkHTML.])
fi

dnl Setup extra GNOME library flags.

EXTRA_GNOME_LIBS=`gnome-config --libs gnomeui gdk_pixbuf print unicode gal $bonobo`
EXTRA_GNOME_CFLAGS=`gnome-config --cflags gnomeui gdk_pixbuf print unicode gal $bonobo`

AC_SUBST(EXTRA_GNOME_LIBS)
AC_SUBST(EXTRA_GNOME_CFLAGS)

dnl gtkhtmlConf.sh stuff.

GTKHTML_LIBDIR="-L${libdir}"
GTKHTML_INCLUDEDIR="-I${includedir}"
GTKHTML_INCLUDEDIR="$GTKHTML_INCLUDEDIR `$GNOME_CONFIG --cflags gdk_pixbuf print` $GCONF_CFLAGS $PSPELL_CFLAGS"
GTKHTML_LIBS="-lgtkhtml `$GNOME_CONFIG --libs gdk_pixbuf print` $GCONF_LIBS $PSPELL_LIBS"

AC_SUBST(GTKHTML_LIBDIR)
AC_SUBST(GTKHTML_INCLUDEDIR)
AC_SUBST(GTKHTML_LIBS)

dnl Library version information.
 
dnl Increment the following if the interface has additions, changes,
dnl removals.
GTKHTML_CURRENT=5

dnl Increment any time the source changes; set to 0 if you
dnl increment CURRENT.
GTKHTML_REVISION=0

dnl Increment if any interfaces have been added; set to 0
dnl if any interfaces have been removed. removal has 
dnl precedence over adding, so set to 0 if both happened.
GTKHTML_AGE=0

AC_SUBST(GTKHTML_CURRENT)
AC_SUBST(GTKHTML_REVISION)
AC_SUBST(GTKHTML_AGE)

dnl Done.

AC_OUTPUT([
Makefile
gtkhtml.spec
po/Makefile.in
macros/Makefile
src/Makefile
capplet/Makefile
components/Makefile
components/html-editor/Makefile
])
